#!/usr/bin/perl
# lst - list sorted directory contents (depth first)

#
# Program lst "Perl Cookbook" Recipe 9.13
# http://book.chinaz.com/Perl/perl/0596003137_perlckbk2-chp-9-sect-13.html
#
# Modified By     Reason
# 12.04.07 rmills change the formatting of the pathname ($path variable)
# 12.03.07 rmills added the -x option to rename files in sequence
#
use Getopt::Std;
use File::Find;
use File::stat;
use File::Basename;
use User::pwent;
use User::grent;

getopts('lusrcmix')    or die <<DEATH;
Usage: $0 [-mucsril] [dirs ...]
 or    $0 -i [-mucsrl] < filelist

Input format:
    -i  read pathnames from stdin
Output format:
    -l  long listing
Sort on:
    -m  use mtime (modify time) [DEFAULT]
    -u  use atime (access time)
    -c  use ctime (inode change time)
    -s  use size for sorting
Ordering:
    -r  reverse sort
    
Rename:
	-x	rename the files 0001 0002 etc
NB: You may only use select one sorting option at a time.
DEATH
    
unless ($opt_i || @ARGV) { @ARGV = ('.') }

if ($opt_c + $opt_u + $opt_s + $opt_m > 1) {
    die "can only sort on one time or size";
}

$IDX = 'mtime';
$IDX = 'atime' if $opt_u;
$IDX = 'ctime' if $opt_c;
$IDX = 'size'  if $opt_s;

$TIME_IDX = $opt_s ? 'mtime' : $IDX;

*name = *File::Find::name;  # forcibly import that variable

# the $opt_i flag tricks wanted into taking
# its filenames from ARGV instead of being
# called from find.

if ($opt_i) {
     *name = *_;  # $name now alias for $_
     while (<>) { chomp; &wanted; }   # ok, not stdin really
}  else {
    find(\&wanted, @ARGV);
}

# sort the files by their cached times, youngest first
@skeys = sort { $time{$b} <=> $time{$a} } keys %time;

# but flip the order if -r was supplied on command line
@skeys = reverse @skeys if $opt_r;

my $sequence = 0 ;

for (@skeys) {
	my $path = $_ ;
	if ( length($path) > 2 && substr($path,0,2) eq './') {
		$path = substr($path,2) ;
	}
	if ( $opt_x ) {
		my $oldname = $_ ;
		if ( -e $oldname && -f $oldname ) {
			$sequence ++ ;
			my ($base,$dir,$ext) = fileparse($oldname,'\..*') ;
			my $newname = sprintf("%s%04d_%s%s",$dir,$sequence,$base,$ext) ;
			my $cmd = "mv \"$oldname\"  \"$newname\"" ;
			print($cmd . "\n" )  ;
			system($cmd) ;
		#	print "$cmd ; echo base = $base dir = $dir ext = $ext \n" ;
		}
	
		next ;
	}
	
    unless ($opt_l) {  # emulate ls -l, except for permissions
        print "$path\n";
        next;
    }
    $now = localtime $stat{$_}->$TIME_IDX();
    printf "%6d %04o %6d %8s %8s %8d %s %s\n",
        $stat{$_}->ino(),
        $stat{$_}->mode() & 07777,
        $stat{$_}->nlink(),
        user($stat{$_}->uid()),
        group($stat{$_}->gid()),
        $stat{$_}->size(),
        $now, $path;
}

# get stat info on the file, saving the desired
# sort criterion (mtime, atime, ctime, or size)
# in the %time hash indexed by filename.
# if they want a long list, we have to save the
# entire stat object in %stat.  yes, this is a
# hash of objects
sub wanted {
    my $sb = stat($_);  # XXX: should be stat or lstat?
    return unless $sb;
    $time{$name} = $sb->$IDX();  # indirect method call
    $stat{$name} = $sb if $opt_l;
}

# cache user number to name conversions
sub user {
    my $uid = shift;
    $user{$uid} = getpwuid($uid)->name || "#$uid"
        unless defined $user{$uid};
    return $user{$uid};
}

# cache group number to name conversions
sub group {
    my $gid = shift;
    $group{$gid} = getgrgid($gid)->name || "#$gid"
        unless defined $group{$gid};
    return $group{$gid};
}
